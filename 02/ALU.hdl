// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a=x, b=false, sel=zx, out=x0);
    Not16(in=x0, out=Notx0);
    Mux16(a=x0, b=Notx0, sel=nx, out=MuxNotx0);
    Mux16(a=y, b=false, sel=zy, out=y0);
    Not16(in=y0, out=Noty0);
    Mux16(a=y0, b=Noty0, sel=ny, out=MuxNoty0);
    And16(a=MuxNotx0, b=MuxNoty0, out=MuxNotx0AndMuxNoty0);
    Add16(a=MuxNotx0, b=MuxNoty0, out=MuxNotx0AddMuxNoty0);
    Mux16(a=MuxNotx0AndMuxNoty0, b=MuxNotx0AddMuxNoty0, sel=f, out=fout);
    Not16(in=fout, out=Notfout);
    Mux16(a=fout, b=Notfout, sel=no, out=out, out[0..7]=outLowerBits, out[8..15]=outHigherBits, out[15]=outHighestBit);
    Or8Way(in=outLowerBits, out=OroutLowerBits);
    Or8Way(in=outHigherBits, out=OroutHigherBits);
    Or(a=OroutLowerBits, b=OroutHigherBits, out=zrin);
    Not(in=zrin, out=zr);
    Mux(a=false, b=true, sel=outHighestBit, out=ng);



}